plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.mikepenz.aboutlibraries.plugin'
}

android {
  namespace 'com.anissan.battarang'
  buildToolsVersion = '35.0.0-rc4'
  compileSdk 34

  defaultConfig {
    targetSdkVersion 34
    minSdkVersion 21
    versionCode 5 // Don't forget to also update the backend
    versionName '1.7.0'

    // Generate "BuildConfig.*" properties
    Properties buildConfigProperties = new Properties()
    buildConfigProperties.load(new FileInputStream(rootProject.file('local.properties')))

    // Skip auto generated Gradle value
    buildConfigProperties.remove('sdk.dir')

    // Values loaded as characters; adding quotes to make them strings
    buildConfigProperties.each { key, value -> { buildConfigField('String', key, "\"$value\"") } }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }

    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      ndk {
        debugSymbolLevel 'FULL'
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = '11'
  }

  buildFeatures {
    buildConfig = true
    viewBinding true
  }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'
  implementation 'org.jetbrains:annotations:24.1.0'

  implementation 'androidx.appcompat:appcompat:1.7.0'
  implementation 'androidx.core:core-ktx:1.13.1'
  implementation 'androidx.fragment:fragment-ktx:1.7.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.viewpager2:viewpager2:1.1.0'
  implementation 'com.google.android.material:material:1.10.0'

  // `com.google.common.util.concurrent.ListenableFuture` imported in PermissionTabViewHolder
  implementation 'com.google.guava:guava:33.2.0-android'

  implementation 'io.insert-koin:koin-android:3.5.6'
  implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
  implementation 'io.github.g00fy2.quickie:quickie-bundled:1.10.0'
  implementation 'dev.chrisbanes.insetter:insetter:0.6.1'
  implementation 'com.github.judemanutd:autostarter:1.1.0'
  implementation 'com.mikepenz:aboutlibraries:10.9.2'
  implementation 'hu.autsoft:krate:2.0.0'

  // This snapshot version can set the app name for Doki API to use in instructions
  implementation 'dev.doubledot.doki:library:c18efeb'
}
